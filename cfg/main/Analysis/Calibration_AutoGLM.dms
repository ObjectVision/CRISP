container Calibration : using = "geography;SourceData/RegionalUnits;SourceData;Classifications;"
{
	unit <uint32> Create_DataForCalibration := CompactedDomain
	{
		attribute<Countries>  Country_id                              := Countries/per_domain[CompactedDomain/org_rel];
		attribute<float32>	  latitude_factor                         := lat_factor;
		attribute<float32>    BU_RES_2000                             := SourceData/Builtup/Residential/Y2000[CompactedDomain/org_rel];
		attribute<float32>    BU_RES_2020                             := SourceData/Builtup/Residential/Y2020[CompactedDomain/org_rel];
		attribute<float32>    BUTOT_2000                              := SourceData/Builtup/Total/Y2000[CompactedDomain/org_rel];
		attribute<float32>    BUTOT_2020                              := SourceData/Builtup/Total/Y2020[CompactedDomain/org_rel];
		attribute<float32>	  BUPERCTOT_2000                          := makedefined(BUTOT_2000 / (1000f * 1000f * latitude_factor), 0f);
		attribute<float32>	  BUPERCTOT_2020                          := makedefined(BUTOT_2020 / (1000f * 1000f * latitude_factor), 0f);
		attribute<float64>    Pop_2000                                := SourceData/Population/PerYear_unrounded/Y2000[CompactedDomain/org_rel];
		attribute<float64>    Pop_2020                                := SourceData/Population/PerYear_unrounded/Y2020[CompactedDomain/org_rel];
		attribute<float64>    PopulationDensity_2000_8dir             := Preprocessing/Population_Neighbourhoods/PopulationDensity_8dir_Y2000/Mean_8dir[CompactedDomain/org_rel];
		attribute<float64>    PopulationDensity_2020_8dir             := Preprocessing/Population_Neighbourhoods/PopulationDensity_8dir_StartYear/Mean_8dir[CompactedDomain/org_rel];
		
		attribute<int32>      Elevation                               := Physical/Elevation/Mean_1k[CompactedDomain/org_rel];
		attribute<float32>    Slope                                   := Physical/Slope_2021/ReadData[CompactedDomain/org_rel];
		attribute<float32>    TRI_mean                                := makedefined(Physical/TerrainRoughnessIndex/TRI_mean_30ss_World[CompactedDomain/org_rel], 1f);
		
		attribute<uint8>      IsProtectedArea                         := Policy/ProtectedAreas/ReadData[CompactedDomain/org_rel];
		attribute<uint8>      IsFloodProneArea_RP100                  := makedefined(Physical/Water/FloodProneArea/RP100/ReadData[CompactedDomain/org_rel], 1[uint8]);
		
		attribute<uint8>      Earthquakes_MMI_Index                   := Physical/Natural_Hazards/Earthquake/Earthquakes_Modified_Mercalli_Intensity/ReadData[CompactedDomain/org_rel];
		attribute<int16>      LandSlide_risk  	 			          := Physical/Natural_Hazards/Landslide/LS_risk[CompactedDomain/org_rel];
		
		attribute<float32>    Distance2Coastline                      := Physical/Coast/Location2Coast_m_sqr/Result[CompactedDomain/org_rel];
		attribute<float32>    Distance2AnyInlandWater                 := Physical/Water/Location2AnyInlandWater_m_sqr/Result[CompactedDomain/org_rel];
		attribute<float32>    Distance2LargeInlandWater               := Physical/Water/Location2LargeInlandWater_m_sqr/Result[CompactedDomain/org_rel];
		attribute<float32>    Distance2MajorRoads                     := Socioeconomic/Infrastructure/Location2Roads123_m_sqr/Result[CompactedDomain/org_rel];
		attribute<float32>    Distance2SecundaryRoads                 := Socioeconomic/Infrastructure/Location2Roads45_m_sqr/Result[CompactedDomain/org_rel];
		
		//attribute<float32>    Distance2Villages_GridBased             := Preprocessing/GridDists/AccessGrids_grid/villages/costsgrid_untiled_maximp_latitude_specific[CompactedDomain/org_rel];
		//attribute<float32>    Distance2Town_GridBased                 := Preprocessing/GridDists/AccessGrids_grid/towns/costsgrid_untiled_maximp_latitude_specific[CompactedDomain/org_rel];
		//attribute<float32>    Distance2Cities_GridBased               := Preprocessing/GridDists/AccessGrids_grid/cities/costsgrid_untiled_maximp_latitude_specific[CompactedDomain/org_rel];
		//attribute<float32>    Distance2Villages_NetworkBased          := Preprocessing/GridDists/AccessGrids_nw/villages/distancegrid[CompactedDomain/org_rel];
		//attribute<float32>    Distance2Town_NetworkBased              := Preprocessing/GridDists/AccessGrids_nw/towns/distancegrid[CompactedDomain/org_rel];
		//attribute<float32>    Distance2Cities_NetworkBased            := Preprocessing/GridDists/AccessGrids_nw/cities/distancegrid[CompactedDomain/org_rel];
		  
		
		
		// needs to be first of containers with template variables
		// container access_to_settlements   := for_each_nedv('Road_distance_atleast_to_'+Socioeconomic/access_to_settlements/gridslist/label, 'Socioeconomic/access_to_settlements/readgrids/'+Socioeconomic/access_to_settlements/gridslist/label+'/infile/ReadData_LL[CompactedDomain/org_rel]', CompactedDomain, uint32);
		container gridcost_to_settlements := for_each_nedv('Grid_costs_atleast_to_'+Accessibility_Variants/Label, 'uint32(Preprocessing/GridDistances/Y2000/Grid_costs_atleast_to/'+Accessibility_Variants/Label+'[CompactedDomain/org_rel])', CompactedDomain, uint32);
		//container pots_pop_2000           := for_each_nedv(pot_fields/label, 'round(SourceData/Population/'+pot_fields/label+'/Y2000[CompactedDomain/org_rel])', CompactedDomain, int32);
		//container pots_pop_2020           := for_each_nedv(pot_fields/label, 'round(SourceData/Population/'+pot_fields/label+'/Y2020[CompactedDomain/org_rel])', CompactedDomain, int32);
	}
	container Export_DataForCalibration 
	{
		unit<uint32> properties : nrofrows = 2 
		{
			attribute<string> name: ['name', 'DomainUnit'];
		}
		  
		unit<uint32> directnames := SubItem_PropValues(Create_DataForCalibration, properties/name) 
		{
			attribute<bool> isvalue:= DomainUnit = '.';
		}
		
		//parameter<string> fieldnamelist:= aslist(directnames/name, fieldseparator)+fieldseparator+aslist(Socioeconomic/access_to_settlements/gridslist/label, fieldseparator);
		parameter<string> fieldnamelist:= aslist(directnames/isvalue ? directnames/name : '', fieldseparator)
		//	+fieldseparator+aslist('Road_distance_atleast_to_'+Socioeconomic/access_to_settlements/gridslist/label, fieldseparator)
			+fieldseparator+aslist('Grid_costs_atleast_to_'+Accessibility_Variants/Label, fieldseparator)
		//	+fieldseparator+aslist('pop2000_'+pot_fields/label, fieldseparator)
		//	+fieldseparator+aslist('pop2020_'+pot_fields/label, fieldseparator)	
			;
		//parameter<string> fieldreflist:= aslist('Create_DataForCalibration/'+directnames/name, fieldseparator)+fieldseparator+aslist('Create_DataForCalibration/access_to_settlements/'+Socioeconomic/access_to_settlements/gridslist/label, fieldseparator);
		parameter<string> fieldreflist:= aslist(directnames/isvalue ? 'Create_DataForCalibration/'+directnames/name : '', fieldseparator)
		//	+fieldseparator+aslist('Create_DataForCalibration/access_to_settlements/Road_distance_atleast_to_'+Socioeconomic/access_to_settlements/gridslist/label, fieldseparator)
			+fieldseparator+aslist('Create_DataForCalibration/gridcost_to_settlements/Grid_costs_atleast_to_'+Accessibility_Variants/Label, fieldseparator)
		//	+fieldseparator+aslist('Create_DataForCalibration/pots_pop_2000/'+pot_fields/label, fieldseparator)
		//	+fieldseparator+aslist('Create_DataForCalibration/pots_pop_2020/'+pot_fields/label, fieldseparator)
			;

		// assume ExportDomain is a configured domain unit with two attributes as subitems: id & dist
		parameter<string> fieldseparator := ';';
		parameter<string> filename       := '%LocalDataProjDir%/Calibration_export/'+ModelParameters/StudyArea+'/calibset.csv';

		unit<uint32> field := range(uint32, 0,strcount(fieldnamelist, ';') +1)
		{
			attribute<string> name := ReadArray(fieldnamelist, ., string, 0);
			attribute<string> ref := ReadArray(fieldreflist, ., string, 0);
		}

		parameter<string> newline := '\n';
		parameter<string> header  := fieldnamelist;
		attribute<string> body (Create_DataForCalibration) := = AsList(
			+'makedefined(string(' + field/ref + '),'+quote('-9999')+')',' + '+quote(fieldseparator)+' +'
			);
		 
		  parameter<string> result := header + newline + AsList(body, newline)
		  ,  StorageName = "= filename"
		  ,  StorageType = "str";
		
		container Export_Tiffs_ForCalibration:= for_each_nedva(field/name, 'uint32('+field/ref+'[CompactedDomain/BaseGrid])',domain,uint32,'%LocalDataProjDir%/Calibration_export/'+ModelParameters/StudyArea+'/tiffs/'+field/name+'.tif');
	}
	
	container Export_BuiltUp_percentages_matrix {
		parameter<string> fieldseparator := ';';
		parameter<string> filename       := '%LocalDataProjDir%/Calibration_export/'+ModelParameters/StudyArea+'/percentages_matrix_'+ModelParameters/StudyArea+'.csv';

		unit<uint32> field: nrofrows = 4
		{
			attribute<string> name : ['first_rel', 'second_rel', 'n', 'rel'];
		}

		parameter<string> newline := '\n';
		parameter<string> header  := aslist(field/name, ';');
		attribute<string> body (Builtup/percentage_matrix) := = AsList(
			+'makedefined(string(Builtup/percentage_matrix/' + field/name + '),'+quote('-9999')+')',' + '+quote(fieldseparator)+' +'
			);
		 
		  parameter<string> result := header + newline + AsList(body, newline)
		  ,  StorageName = "= filename"
		  ,  StorageType = "str";
	}
	
	container ReadCoffecients
	{
		unit<uint8> Continents_InCalib : nrofrows = 6
		{
			attribute<string> name : ['Africa','Asia','Australia_Oceania','Europe','North_America','South_America'];

			attribute<string> label := name;
			attribute<Classifications/Continents> cont_rel:= rlookup(label, Classifications/Continents/name);
			container V := for_each_nedv(AsItemName(name), String(ID(.))+'[..]', void, .);
		}
		
		container Read_XLSX 
		: StorageName = "='%ProjDir%/Data/Calibration/Calibration_continents_'+ModelParameters/Calibration_filedate+'.xlsx'"
		, StorageType = "gdal.vect"
		, StorageReadOnly = "true"
		, SyncMode = "AllTables";
		
		container autoglm:= get_coefs_T(Read_XLSX/AutoGLM);
		container glm2:= get_coefs_T(Read_XLSX/glm2);
		container beta:= get_coefs_T(Read_XLSX/beta);
		
		container Read_Cubist:= for_each_ne(Continents_InCalib/name, 'Templates/ReadCubistModel('+quote('%ProjDir%/Data/Calibration/cubist_'+Continents_InCalib/name+'_'+ModelParameters/Calibration_filedate+'.csv')+')') {
			unit<uint8> Continental_models:= Classifications/Continents  {
				attribute<string> ModelsList:= ='union_data(., '+refs+')';
				attribute<string> RulesList:= ='union_data(., '+rules+')';
				parameter<string> refs:= asitemlist(Classifications/Continents/name+'/rules/exec_param');
				parameter<string> rules:= asitemlist(Classifications/Continents/name+'/rules/exec_rules');
				parameter<string> exec_param:= 'add('+asitemlist('(float32(domain/Continent_rel == '+string(id(.))+'b) * '+ModelsList+')')+')';
				parameter<string> exec_rules:= 'add('+asitemlist('(float32(domain/Continent_rel == '+string(id(.))+'b) * '+RulesList+')')+')';
			}
		}
		
		template get_coefs_T {
		
			unit<uint32> in_set;
			
			unit<uint32> Calibration_variables := in_set
			{
				attribute<string> name := AsItemName(Variables);
				container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
			}
		
			container PerContinent := 
				for_each_nedv(
					Continents_InCalib/name
					, 'Calibration_variables/' + Continents_InCalib/name + '[float32]'
					, Calibration_variables
					, float32
				)
			{
				attribute<string> var_name (Calibration_variables) := Calibration_variables/name;
			}

			container PerVar := 
				for_each_nedv(
					Calibration_variables/name
					, 'union_data(Continents_InCalib,'+replace(AsItemList('PerContinent/'+Continents_InCalib/name+'[Calibration_variables/V/@CV@]'),'@CV@', Calibration_variables/Name)+')'
					, Continents_InCalib
					, float32
				);
			
			container Coef2domain := Coef2domain_T(PerVar);
		}
		unit<uint32> Variables_Glossary: nrofrows = 23 {
		
			attribute<string> Alias:= Elements/inval[((id(.)*3)+0)[Elements]];
			attribute<string> Description:= Elements/inval[((id(.)*3)+1)[Elements]];
			attribute<string> Address:= Elements/inval[((id(.)*3)+2)[Elements]];
			attribute<string> LabelText:= Alias;
			
			unit<uint32> Elements:= range(uint32, 0, #Variables_Glossary*3) {
			
				attribute<string> inval: [
					//'Calibration Alias',	'Description',	'Model address',
					'Intercept',	'Constant value',	'const(1f,domain,float32)',
					'CurrBuState',	'Current built-up value',	'recollect_by_cond(domain/IsCompactedDomain,  CurrBuState/BuiltUp_Total_Share)[float32]',
					'BuPercTot_2000',	'Built-up value in 2000',	'recollect_by_cond(domain/IsCompactedDomain,  CurrBuState/BuiltUp_Total_Share)[float32]',
					'ln_PopulationDensity_2000_8dir',	'Current neighbourhood population density',	'CurrPopState/PopulationDensity_8dir/Ln_mean_8dir[float32]',
					'Distance2Coastline',	'Distance to coastline',	'min_elem(Physical/Coast/Location2Coast_m_sqr/Result[float32], 250f)',
					'Distance2LargeInlandWater',	'Distance to large inland waterbody',	'min_elem(Physical/Water/Location2LargeInlandWater_m_sqr/Result[float32], 250f)',
					'Distance2MajorRoads',	'Distance to major roads',	'min_elem(makedefined(Socioeconomic/Infrastructure/Location2Roads123_m_sqr/Result[float32],1000f),250f)',
					'Distance2SecundaryRoads',	'Distance to secondary roads',	'min_elem(makedefined(Socioeconomic/Infrastructure/Location2Roads45_m_sqr/Result[float32],1000f),250f)',
					'Distance2SecondaryRoads',	'Distance to secondary roads',	'min_elem(makedefined(Socioeconomic/Infrastructure/Location2Roads45_m_sqr/Result[float32],1000f),250f)',
					'Grid_costs_atleast_to_cities',	'Grid cost to closest city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/cities',
					'gc_cities',	'Grid cost to closest city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/cities',
					'Grid_costs_atleast_to_towns',	'Grid cost to closest town or city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/towns',
					'gc_towns',	'Grid cost to closest town or city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/towns',
					'Grid_costs_atleast_to_villages',	'Grid cost to closest village, towns, or city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/villages',
					'gc_villages',	'Grid cost to closest village, towns, or city',	'Calculate_GridDistances/PREVYEAR/Grid_costs_atleast_to/villages',
					'Elevation',	'Elevation',	'Physical/Elevation/Mean_1k[float32]',
					'slope',	'Slope',	'Physical/Slope_2021/ReadData[float32]',
					'TRI_mean',	'TRI',	'makedefined(Physical/TerrainRoughnessIndex/TRI_mean_30ss_World[float32], 0f)',
					'IsProtectedArea',	'Is a protected area',	'Policy/ProtectedAreas/ReadData[float32]',
					'IsFloodProneArea_RP100',	'Is flood prone  (100y return period)',	'Physical/Water/FloodProneArea/RP100/ReadData_Defined[float32]',
					'Earthquakes_MMI_Index',	'Earthquakes occurence (MMI index)',	'Physical/Natural_Hazards/Earthquake/Earthquakes_Modified_Mercalli_Intensity/ReadData[float32]',
					'LandSlideProne_ARUP',	'Landslides likeliness (ARUP index)',	'Physical/Natural_Hazards/Landslide/LS_risk[float32]',
					'Landslide_prone',	'Landslides likeliness (ARUP index)',	'Physical/Natural_Hazards/Landslide/LS_risk[float32]'
				];
			}
		}
	}

	Template Coef2domain_T
	{
		container Continent;
		///
		
		attribute<uint8>   Continent_id (Countries)   := rlookup(Countries/Continent_name, Continents_InCalib/name);
		
		container Country := 
			for_each_nedv(
				Calibration_variables/name
				, 'Continent/'+Calibration_variables/name+'[Continent_id]'
				, Countries 
				, float32
			);
			
		container Domain := 
			for_each_nedv(
				Calibration_variables/name
				, 'MakeDefined(Country/'+Calibration_variables/name+'[Countries/per_domain],0f)'
				, geography/domain 
				, float32
			);
	}
}
